//
// Main build file for Randoms
plugins {
    id "java"
}

task run (type: JavaExec, dependsOn: classes){
    if(project.hasProperty('appargs')){
        args(appargs.split(','))
    }
    description = "Run Randoms Service"
    main = "randoms.RandomService"
    classpath = sourceSets.main.runtimeClasspath
}

jar {
    manifest {
        attributes "Main-Class": "randoms.RandomService"
    }

    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it).matching {
                exclude {
                    it.path.contains("META-INF/ECLIPSE_") ||
                    it.path.contains("module-info.class") ||
                    it.path.contains("mozilla/public-suffix-list.txt")
                }
            }
        }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.sparkjava:spark-core:2.5.3"
    runtimeOnly "org.slf4j:slf4j-api:2.0.7"
    runtimeOnly "org.slf4j:slf4j-nop:2.0.7"
}
